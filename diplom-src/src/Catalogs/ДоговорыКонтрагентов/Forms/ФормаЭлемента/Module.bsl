&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	 НаборЗаписей = РегистрыСведений.ВКМ_АбонентскаяРабота.СоздатьНаборЗаписей();
	 НаборЗаписей.Отбор.Объект.Установить(ТекущийОбъект.Ссылка);
	
	 Для Каждого ОписанияСвойства Из ВКМ_ПереченьСвойств Цикл 
		 
		 Запись = НаборЗаписей.Добавить();
		 Запись.Объект = ТекущийОбъект.Ссылка;
		 Запись.ИмяРеквизита = ОписанияСвойства.ИмяРеквизита;
		 Значение = ЭтотОбъект[ОписанияСвойства.ИмяРеквизита];
		 Если ТипЗнч(Значение) = Тип("СтандартныйПериод") Тогда
		 	Запись.ДатаНачала = Значение.ДатаНачала;
		 	Запись.ДатаОкончания = Значение.ДатаОкончания;
		 Иначе
		 	Запись.Значение = Значение;
		 КонецЕсли;
		 		 	 
	 КонецЦикла;
	 
	 НаборЗаписей.Записать();
	 
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	СоздаваемыеРеквизитыФормы = Новый Соответствие;
	
	
	СоздаваемыеРеквизитыФормы.Вставить("ВКМ_Период_Действия_Договора", Новый ОписаниеТипов("СтандартныйПериод"));
	СоздаваемыеРеквизитыФормы.Вставить("ВКМ_Суммы_Абонентской_Платы", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	СоздаваемыеРеквизитыФормы.Вставить("ВКМ_Стоимость_Часа_Работы", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(10, 2, ДопустимыйЗнак.Неотрицательный)));
	
	ГруппаНаформе = Элементы.Добавить("ВКМ_ГруппаДобовляемыхРеквизитов", Тип("ГруппаФормы"), ЭтотОбъект);	
	ГруппаНаформе.Вид = ВидГруппыФормы.ОбычнаяГруппа;	
    ГруппаНаформе.Заголовок = "Добавочные реквизиты";
    ГруппаНаформе.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ДобавлянмыеРеквизиты = Новый Массив;
  
    Для Каждого Запись Из  СоздаваемыеРеквизитыФормы Цикл
    	
        ИмяРеквизита = Запись.Ключ;	  
	    Тип = Запись.Значение;    	
    	
    	НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита,Тип);	    	    
	   	ДобавлянмыеРеквизиты.Добавить(НовыйРеквизит);
		
		ОписаниеСвойства = ВКМ_ПереченьСвойств.Добавить();
		ОписаниеСвойства.ИмяРеквизита = ИмяРеквизита;
				
    КонецЦикла;
    	
	ИзменитьРеквизиты(ДобавлянмыеРеквизиты);
	
	Для Каждого Свойство Из ВКМ_ПереченьСвойств Цикл
		 
		НаименованиеРеквизита = Сред(Свойство.ИмяРеквизита, 5,25);
	    НаименованиеРеквизитаМассив = СтрРазделить(НаименованиеРеквизита,"_" );
	    НаименованиеРеквизита = НаименованиеРеквизитаМассив[0] + " " + НРег(НаименованиеРеквизитаМассив[1]) + " " + НРег(НаименованиеРеквизитаМассив[2]);
	    		
		Элемент = Элементы.Добавить(Свойство.ИмяРеквизита,Тип("ПолеФормы"),ГруппаНаформе);
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.ПутьКДанным = Свойство.ИмяРеквизита;
		Элемент.Заголовок = НаименованиеРеквизита;		
		
	КонецЦикла;  
	
	Если Не Параметры.Ключ.Пустая() Тогда
		ПрочитатьЗначенияСвойств();
	КонецЕсли;	

КонецПроцедуры


&НаСервере
Процедура ПрочитатьЗначенияСвойств()	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_АбонентскаяРабота.Объект,
	|	ВКМ_АбонентскаяРабота.ИмяРеквизита,
	|	ВКМ_АбонентскаяРабота.Значение,
	|	ВКМ_АбонентскаяРабота.ДатаНачала,
	|	ВКМ_АбонентскаяРабота.ДатаОкончания
	|ИЗ
	|	РегистрСведений.ВКМ_АбонентскаяРабота КАК ВКМ_АбонентскаяРабота
	|ГДЕ
	|	ВКМ_АбонентскаяРабота.Объект = &Объект";
	
	Запрос.УстановитьПараметр("Объект", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ЗначениеСвойств = Новый Структура;
	
	Пока Выборка.Следующий() Цикл
				
		Если  Выборка.ДатаНачала > Дата("19000101") Тогда	
			ДатаНачала = Выборка.ДатаНачала;
			ДатаОкончания = Выборка.ДатаОкончания;		
			ЗначениеСвойств.Вставить(Выборка.ИмяРеквизита, Новый СтандартныйПериод(ДатаНачала,ДатаОкончания));
		Иначе	
		    ЗначениеСвойств.Вставить(Выборка.ИмяРеквизита, Выборка.Значение);
		КонецЕсли;
						
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,ЗначениеСвойств);
	
КонецПроцедуры


